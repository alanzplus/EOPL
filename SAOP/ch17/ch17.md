# Chapter 17 Using Continuations

## 17.5 Escaping From and Returning to Deep Recursions

### Break Point Utility
Based on the ideas of examples, create a `break` function for breakpoint like debugging.

`break` suspends the procedure execution and it allows view/change the value of variable or expression.

```scheme
(define escaper "undefined")
(let ([r (lambda (cont)
           (set! escaper
             (lambda (proc)
               (lambda args
                 (cont (lambda () (apply proc args))))))
           (lambda () writeln "escaper is defined"))])
  ((call/cc r)))

(define resume "undefined")
(define break-argument "undefined")
(define view (lambda () ((car break-argument))))
(define change (lambda (v) ((cadr break-argument) v)))
(define break
  (lambda (x)
    (let ([r (lambda (cont)
               (set! resume (lambda () (cont (view))))
               (set! break-argument x)
               ((escaper (lambda () (view)))))])
      (call/cc r))))
```

Define syntax `breakpoint` which allows change the value of variable.

```scheme
(require (for-syntax racket/match))
(define-syntax (breakpoint stx)
  (match (syntax->list stx)
         [(list _ arg)
          (let ([t
                  (if (identifier? arg)
                      `(break (list
                                (lambda () ,arg)
                                (lambda (v) (set! ,arg v))))
                      `(break (let ([tmp ,arg])
                                (list
                                  (lambda () tmp)
                                  (lambda (v) (set! tmp v))))))])
                  (datum->syntax stx t))]))
```

Use `breakpoint` on function `flatten-number-list`

```scheme
(define flatten-number-list
  (lambda (s)
    (cond
      [(null? s) '()]
      [(number? s) (list s)]
      [else
        (append
          (flatten-number-list (breakpoint (car s)))
          (flatten-number-list (cdr s)))])))
```

```
> (flatten-number-list '(1 2 3))
'(1)
> (view)
1
> (change '(1984 4894))
> (resume)
'(1984)
> (resume)
'(4891)
> (resume)
'(2)
> (resume)
'(3)
> (resume)
'(1984 4891 2 3)
```

## 17.6 Coroutines: Continuation In Action

A rewrite of the example given in the book and it is also the solution to exercise 17.10 and 17.11.

```scheme
(define blanks (lambda (n) (make-string n #\ )))

(define puts
  (lambda args (display (string-join args " " #:after-last "\n"))))

(define coroutine-maker
  (lambda (proc)
    (letrec ([saved-continuation null]
             [resumer (lambda (next-coroutine value)
                        (call/cc (lambda (cont)
                                   (set! saved-continuation cont)
                                   (next-coroutine value))))])
      (lambda (value)
        (if (null? saved-continuation)
          (proc resumer value)
          (saved-continuation value))))))

(define A
  (let ([A-proc (lambda (resume v)
                  (puts "This is A")
                  (puts "Came from" (resume B "A"))
                  (puts "Back in A")
                  (puts "Came from" (resume C "A")))])
    (coroutine-maker A-proc)))

(define B
  (let ([B-proc (lambda (resume v)
                  (puts (blanks 14) "This is B" )
                  (puts (blanks 14) "Came from"  (resume C "B"))
                  (puts (blanks 14) "Back in B" )
                  (puts (blanks 14) "Came from"  (resume A "B")))])
    (coroutine-maker B-proc)))

(define C
  (let ([C-proc (lambda (resume v)
                  (puts (blanks 28) "This is C" )
                  (puts (blanks 28) "Came from"  (resume A "C"))
                  (puts (blanks 28) "Back in C" )
                  (puts (blanks 29) "Came from"  (resume B "C")))])
    (coroutine-maker C-proc)))
```

## 17.7 Grune's Problem

Rewrite of example given in the book and it also contains the solution to 17.13, 17.14 and 17.15.

```scheme
(require readline/readline)
(require (for-syntax racket/match))

(define prompt-read
  (lambda (prompt)
    (string->symbol (readline prompt))))

(define blanks (lambda (n) (make-string n #\ )))

(define puts
  (lambda args (display (string-join args " " #:after-last "\n"))))

(define-syntax (wrap stx)
  (match (syntax->list stx)
         [(list _ proc)
          (let ([t `(let ([p (lambda () ,proc)])
                      (lambda args
                        (apply (p) args)))])
            (datum->syntax stx t))]))

(define coroutine-maker
  (lambda (proc)
    (letrec ([saved-continuation null]
             [resumer (lambda (next-coroutine value)
                        (call/cc (lambda (cont)
                                   (set! saved-continuation cont)
                                   (next-coroutine value))))])
      (lambda (value)
        (if (null? saved-continuation)
          (proc resumer value)
          (saved-continuation value))))))

(define cycle-proc
  (lambda (proc)
    (let ([cc (call/cc (lambda (cont) cont))])
      (proc)
      (cc cc))))

(define reader
  (lambda (right)
    (coroutine-maker (lambda (resume v)
                       (cycle-proc
                         (lambda ()
                           (let ([r (prompt-read "in>")])
                             (printf "~s\n" r)
                             (resume right r))))))))

(define writer
  (lambda (left escape-on-end)
    (coroutine-maker (lambda (resume v)
                        (cycle-proc
                          (lambda ()
                            (let ([s (resume left 'ok)])
                              (if (eq? s 'end)
                                  (escape-on-end 'end)
                                  (puts "out>" (symbol->string s))))))))))

(define x->y
  (lambda (x y left right)
    (let ([proc (lambda (resume v)
                  (cycle-proc
                    (lambda ()
                      (let ([s1 (resume left 'ok)])
                        (if (eq? s1 x)
                            (let ([s2 (resume left 'more)])
                              (if (eq? s2 x)
                                  (resume right y)
                                  (begin
                                    (resume right s1)
                                    (resume right s2))))
                            (resume right s1))))))])
      (coroutine-maker proc))))

(define grune
  (lambda ()
    (call/cc (lambda (cont)
               (letrec
                 ([Input (reader (wrap A))]
                  [A (x->y 'a 'b (wrap Input) (wrap B))]
                  [B (x->y 'b 'c (wrap A) (wrap Output))]
                  [Output (writer (wrap B) cont)])
                 (Output 'ok))))))
```

## Exercise 
### 17.1
**Description**

> Rewrite cycle-proc using continuations instead of recursion as presented in Program 14.11.

> Program 14.11

```scheme
(define cycle-proc
  (lambda (th)
    (letrec ([loop (lambda ()
                     (th)
                     (loop))])
      (loop))))
```

**Solution**

```scheme
(define cycle-proc
  (lambda (th)
    (let ([cc (call/cc (lambda (cont) cont))])
      (th)
      (cc cc))))
```

### 17.3
**Description**

> Determine what this expression represents:

[1]

```scheme
(let ([receiver (lambda (continutation)
                  (call/cc continutation))])
  (call/cc receiver))
```

[2]

> What is (call/cc call/cc) ?

**Solution**

[1]

```
(call/cc receiver)

=>

(receiver (escaper (lambda (val) val)))

=>

(call/cc (escaper (lambda (val) val)))

=>

(
  (escaper (lambda (val) val))
  (escaper (lambda (val) val)))
```

[2]

```
(call/cc call/cc)

=>

(call/cc (escaper (lambda (val) val)))

=>

(
  (escaper (lambda (val) val))
  (escaper (lambda (val) val)))
```

### 17.7

**Solution**

```scheme
(define escaper "undefined")
(let ([r (lambda (cont)
           (set! escaper
             (lambda (proc)
               (lambda args
                 (cont (lambda () (apply proc args))))))
            (lambda () (writeln "escaper is defined")))])
  ((call/cc r)))

(define num-zero 0)
(define product+
  (lambda (n ls)
    (set! num-zero 0)
    (letrec ([product
               (lambda (ls)
                 (cond
                   [(null? ls) 1]
                   [(number? (car ls))
                    (*
                      (if (zero? (car ls)) (break-on-zero) (car ls))
                      (product (cdr ls)))]
                   [else
                     (*
                       (product (car ls))
                       (product (cdr ls)))]))])
             (+ n (product ls)))))

(define get-back "undefined")
(define break-on-zero
    (lambda ()
      (call/cc (lambda (cont)
                 (set! get-back (lambda (v) (cont v)))
                 (set! num-zero (+ num-zero 1))
                 (if (eqv? num-zero 3)
                     ((escaper (lambda () (displayln "error: too many zeros"))))
                     ((escaper (lambda () 0))))))))
```

### 17.8

**Solution**

```scheme
(define escaper "undefined")
(let ([r (lambda (cont)
           (set! escaper
             (lambda (proc)
               (lambda args
                 (cont (lambda () (apply proc args))))))
           (lambda () writeln "escaper is defined"))])
  ((call/cc r)))

(define resume-stack '())
(define resume
  (lambda (v)
    (let ([curr (car resume-stack)])
      (set! resume-stack (cdr resume-stack))
      (curr v))))
(define break-argument "undefined")
(define view (lambda () ((car break-argument))))
(define change (lambda (v) ((cadr break-argument) v)))
(define break
  (lambda (x)
    (let ([r (lambda (cont)
               (set! resume-stack (cons (lambda (v) (cont v)) resume-stack))
               (set! break-argument x)
               ((escaper (lambda () (view)))))])
      (call/cc r))))

(require (for-syntax racket/match))
(define-syntax (breakpoint stx)
  (match (syntax->list stx)
         [(list _ arg)
          (let ([t
                  (if (identifier? arg)
                      `(break (list
                                (lambda () ,arg)
                                (lambda (v) (set! ,arg v))))
                      `(break (let ([tmp ,arg])
                                (list
                                  (lambda () tmp)
                                  (lambda (v) (set! tmp v))))))])
                  (datum->syntax stx t))]))

(define flatten-number-list
  (lambda (s)
    (cond
      [(null? s) '()]
      [(number? s) (list (breakpoint s))]
      [else
        (append
          (flatten-number-list (car s))
          (flatten-number-list (cdr s)))])))
```

### 17.13
**Description**

> Consider the special form `wrap`, which has the following syntax table entry:

```
(wrap proc) = (lambda args (apply proc args))
```

> This works in all cases but one: when `args` is free variable in the `proc` expression. Rewrite wrap using thunks to avoid this potentail free variable.

**Solution**

```scheme
(require (for-syntax racket/match))
(define-syntax (wrap stx)
  (match (syntax->list stx)
         [(list _ proc)
          (let ([t `(let ([p (lambda () ,proc)])
                      (lambda args
                        (apply (p) args)))])
            (datum->syntax stx t))]))
```

We can test by

```scheme
; it outputs "free variable" when using the rewritten version
; it outputs "argument" when using the version given in the description
(let ([args "free variable"])
  ((wrap (lambda (v) args)) "argument")) 
```

### 17.16

**Solution**

```scheme
(require (for-syntax racket/match))
(define-syntax (safe-letrec stx)
  (let ([slist (syntax->list stx)])
  (letrec ([ids
             (match slist
                    [(list _ inits _)
           (map (lambda (e) (syntax->datum (car (syntax->list e)))) (syntax->list inits))])]
           [transform 
             (lambda (expr ids)
               (cond
                 [(identifier? expr)
                  (if (member (syntax->datum expr) ids) `(wrap ,expr) expr)]
                 [(syntax->list expr)
                  (map (lambda (e) (transform e ids)) (syntax->list expr))]
                 [else expr]))])
    (datum->syntax stx
                   (match slist
                          [(list _ inits body)
                           `(letrec
                              ,(map (lambda (e)
                                      (let ([l (syntax->list e)])
                                        `(,(car l) ,(transform (cadr l) ids))))
                                    (syntax->list inits))
                              ,body)])))))
```

Test by

```scheme
(syntax->datum (expand-once '(safe-letrec
                               ((Input (reader A))
                                (A (x->y 'a 'b Input B))
                                (B (x->y 'b 'c A Output))
                                (Output (writer B escape-grune)))
                               (Output 'ok))))
```

outputs

```scheme
'(letrec ((Input (reader (wrap A)))
          (A (x->y 'a 'b (wrap Input) (wrap B)))
          (B (x->y 'b 'c (wrap A) (wrap Output)))
          (Output (writer (wrap B) escape-grune)))
   (Output 'ok))
```

TOOD-FIX-ME However it this implementation doesn't work if initialization expression contains a bound variable
has the same name as one of the identifier occurs in left side.

For example,

```scheme
(safe-letrec
  ((a "a")
   (b (lambda (a) a)))
  (b "b"))
```
