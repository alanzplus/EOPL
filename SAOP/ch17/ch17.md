# Chapter 17 Using Continuations

## 17.5 Escaping From and Returning to Deep Recursions

### Break Point Utility
Based on the ideas of examples, create a `break` function for breakpoint like debugging.

`break` suspends the procedure execution and it allows view/change the value of variable or expression.

```racket
(define escaper "undefined")
(let ([r (lambda (cont)
           (set! escaper
             (lambda (proc)
               (lambda args
                 (cont (lambda () (apply proc args))))))
           (lambda () writeln "escaper is defined"))])
  ((call/cc r)))

(define resume "undefined")
(define break-argument "undefined")
(define view (lambda () ((car break-argument))))
(define change (lambda (v) ((cadr break-argument) v)))
(define break
  (lambda (x)
    (let ([r (lambda (cont)
               (set! resume (lambda () (cont (view))))
               (set! break-argument x)
               ((escaper (lambda () (view)))))])
      (call/cc r))))
```

Define syntax `breakpoint` which allows change the value of variable.

```racket
(require (for-syntax racket/match))
(define-syntax (breakpoint stx)
  (match (syntax->list stx)
         [(list _ arg)
          (let ([t
                  (if (identifier? arg)
                      `(break (list
                                (lambda () ,arg)
                                (lambda (v) (set! ,arg v))))
                      `(break (let ([tmp ,arg])
                                (list
                                  (lambda () tmp)
                                  (lambda (v) (set! tmp v))))))])
                  (datum->syntax stx t))]))
```

Use `breakpoint` on function `flatten-number-list`

```racket
(define flatten-number-list
  (lambda (s)
    (cond
      [(null? s) '()]
      [(number? s) (list s)]
      [else
        (append
          (flatten-number-list (breakpoint (car s)))
          (flatten-number-list (cdr s)))])))
```

```
> (flatten-number-list '(1 2 3))
'(1)
> (view)
1
> (change '(1984 4894))
> (resume)
'(1984)
> (resume)
'(4891)
> (resume)
'(2)
> (resume)
'(3)
> (resume)
'(1984 4891 2 3)
```

## 17.6 Coroutines: Continuation In Action

A rewrite of the example given in the book and it is also the solution to exercise 17.10 and 17.11.

```racket
(define blanks (lambda (n) (make-string n #\ )))

(define puts
  (lambda args (display (string-join args " " #:after-last "\n"))))

(define coroutine-maker
  (lambda (proc)
    (letrec ([saved-continuation null]
             [resumer (lambda (next-coroutine value)
                        (call/cc (lambda (cont)
                                   (set! saved-continuation cont)
                                   (next-coroutine value))))])
      (lambda (value)
        (if (null? saved-continuation)
          (proc resumer value)
          (saved-continuation value))))))

(define A
  (let ([A-proc (lambda (resume v)
                  (puts "This is A")
                  (puts "Came from" (resume B "A"))
                  (puts "Back in A")
                  (puts "Came from" (resume C "A")))])
    (coroutine-maker A-proc)))

(define B
  (let ([B-proc (lambda (resume v)
                  (puts (blanks 14) "This is B" )
                  (puts (blanks 14) "Came from"  (resume C "B"))
                  (puts (blanks 14) "Back in B" )
                  (puts (blanks 14) "Came from"  (resume A "B")))])
    (coroutine-maker B-proc)))

(define C
  (let ([C-proc (lambda (resume v)
                  (puts (blanks 28) "This is C" )
                  (puts (blanks 28) "Came from"  (resume A "C"))
                  (puts (blanks 28) "Back in C" )
                  (puts (blanks 29) "Came from"  (resume B "C")))])
    (coroutine-maker C-proc)))
```

## Exercise 
### 17.1
**Description**

> Rewrite cycle-proc using continuations instead of recursion as presented in Program 14.11.

> Program 14.11

```racket
(define cycle-proc
  (lambda (th)
    (letrec ([loop (lambda ()
                     (th)
                     (loop))])
      (loop))))
```

**Solution**

```racket
(define cycle-proc
  (lambda (th)
    (let ([cc (call/cc (lambda (cont) cont))])
      (th)
      (cc cc))))
```

### 17.3
**Description**

> Determine what this expression represents:

[1]

```racket
(let ([receiver (lambda (continutation)
                  (call/cc continutation))])
  (call/cc receiver))
```

[2]

> What is (call/cc call/cc) ?

**Solution**

[1]

```
(call/cc receiver)

=>

(receiver (escaper (lambda (val) val)))

=>

(call/cc (escaper (lambda (val) val)))

=>

(
  (escaper (lambda (val) val))
  (escaper (lambda (val) val)))
```

[2]

```
(call/cc call/cc)

=>

(call/cc (escaper (lambda (val) val)))

=>

(
  (escaper (lambda (val) val))
  (escaper (lambda (val) val)))
```

### 17.7

**solution**

```racket
(define escaper "undefined")
(let ([r (lambda (cont)
           (set! escaper
             (lambda (proc)
               (lambda args
                 (cont (lambda () (apply proc args))))))
            (lambda () (writeln "escaper is defined")))])
  ((call/cc r)))

(define num-zero 0)
(define product+
  (lambda (n ls)
    (set! num-zero 0)
    (letrec ([product
               (lambda (ls)
                 (cond
                   [(null? ls) 1]
                   [(number? (car ls))
                    (*
                      (if (zero? (car ls)) (break-on-zero) (car ls))
                      (product (cdr ls)))]
                   [else
                     (*
                       (product (car ls))
                       (product (cdr ls)))]))])
             (+ n (product ls)))))

(define get-back "undefined")
(define break-on-zero
    (lambda ()
      (call/cc (lambda (cont)
                 (set! get-back (lambda (v) (cont v)))
                 (set! num-zero (+ num-zero 1))
                 (if (eqv? num-zero 3)
                     ((escaper (lambda () (displayln "error: too many zeros"))))
                     ((escaper (lambda () 0))))))))
```

### 17.8

**solution**

```racket
(define escaper "undefined")
(let ([r (lambda (cont)
           (set! escaper
             (lambda (proc)
               (lambda args
                 (cont (lambda () (apply proc args))))))
           (lambda () writeln "escaper is defined"))])
  ((call/cc r)))

(define resume-stack '())
(define resume
  (lambda (v)
    (let ([curr (car resume-stack)])
      (set! resume-stack (cdr resume-stack))
      (curr v))))
(define break-argument "undefined")
(define view (lambda () ((car break-argument))))
(define change (lambda (v) ((cadr break-argument) v)))
(define break
  (lambda (x)
    (let ([r (lambda (cont)
               (set! resume-stack (cons (lambda (v) (cont v)) resume-stack))
               (set! break-argument x)
               ((escaper (lambda () (view)))))])
      (call/cc r))))

(require (for-syntax racket/match))
(define-syntax (breakpoint stx)
  (match (syntax->list stx)
         [(list _ arg)
          (let ([t
                  (if (identifier? arg)
                      `(break (list
                                (lambda () ,arg)
                                (lambda (v) (set! ,arg v))))
                      `(break (let ([tmp ,arg])
                                (list
                                  (lambda () tmp)
                                  (lambda (v) (set! tmp v))))))])
                  (datum->syntax stx t))]))

(define flatten-number-list
  (lambda (s)
    (cond
      [(null? s) '()]
      [(number? s) (list (breakpoint s))]
      [else
        (append
          (flatten-number-list (car s))
          (flatten-number-list (cdr s)))])))
```
